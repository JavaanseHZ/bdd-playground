plugins {
	id 'org.springframework.boot' version '2.2.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'idea'
	id "de.javaansehz.cluecumber-report-gradle-plugin" version "1.1.4"
    id "io.qameta.allure" version "2.8.1"
}

group = 'de.javaansehz'
version = '0.0.1-SNAPSHOT'

def cucumberVersion = '4.8.0'
def junitVersion = '5.5.0'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compile 'com.h2database:h2:1.4.200'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation "io.cucumber:cucumber-java8:${cucumberVersion}"
	testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
	testImplementation "io.cucumber:cucumber-spring:${cucumberVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
	testImplementation "io.qameta.allure:allure-cucumber4-jvm:2.13.0"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
	testUnitBdd {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}

	testIntegrationBdd {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}

	testUnitBddCompile.extendsFrom testImplementation
	testUnitBddRuntime.extendsFrom testRuntime

	testIntegrationBddCompile.extendsFrom testImplementation
	testIntegrationBddRuntime.extendsFrom testRuntime
	testIntegrationBddRuntime.extendsFrom compile
}

test {
	useJUnitPlatform()
}

task bddUnitTest() {
	dependsOn assemble, compileTestUnitBddJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.testUnitBdd.output
			args = ['--plugin', 'pretty',
					'--plugin', 'junit:build/test-results/cucumber-junit.xml',
					'--plugin', 'json:../reports/bddunit.json',
					'--plugin', 'io.qameta.allure.cucumber4jvm.AllureCucumber4Jvm',
					'--glue', 'de.javaansehz.backend', '../features',
					'--tags', '@unit or @simple']
		}
	}
}

task bddIntegrationTest() {
	dependsOn assemble, compileTestIntegrationBddJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.testIntegrationBdd.output
			args = ['--plugin', 'pretty',
					'--plugin', 'junit:build/test-results/cucumber-springboot.xml',
					'--plugin', 'json:../reports/bddintegration.json',
					'--plugin', 'io.qameta.allure.cucumber4jvm.AllureCucumber4Jvm',
					'--glue', 'de.javaansehz.backend', '../features',
					'--tags', '@integration'
			]
		}
	}
}

cluecumberReports {
	sourceJsonReportDirectory = "../reports"
	generatedHtmlReportDirectory = "../reports"
}

allure {
	autoconfigure = true
	version = '2.4.1'
}
